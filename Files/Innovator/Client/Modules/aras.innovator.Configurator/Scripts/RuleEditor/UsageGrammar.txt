/*
	GrammarInputGroup grammar (Usage expressions parsing)
	Should export two start rules: LexemAnalysis amd ExpressionAnalysis

	Limitations:
		1. Whitespace lexem rule should return 'whitespace' type.
		2. Unknown lexem rule should return 'unknown' type.
*/

{
	var parserInstance = this;
	var usageNameValueCollectionMap = {
		FamilyName: '_FamilyNames', 
		OptionName: '_OptionNames',
		LogicOperator: ['OR', 'AND'],
		OpenBracket: ['('],
		CloseBracket: [')'],
		OpenSquareBracket: ['['],
		CloseSquareBracket: [']'],
		NotLiteral: ['NOT'],
		BooleanOperator: ['='],
		ValidOptionName: '_ValidOptionNames'
	};

	if (!parserInstance.standardGrammarLexemes) {
		parserInstance.standardGrammarLexemes = new Set(
			[].concat(
				usageNameValueCollectionMap.LogicOperator,
				usageNameValueCollectionMap.OpenBracket,
				usageNameValueCollectionMap.CloseBracket,
				usageNameValueCollectionMap.OpenSquareBracket,
				usageNameValueCollectionMap.CloseSquareBracket,
				usageNameValueCollectionMap.NotLiteral,
				usageNameValueCollectionMap.BooleanOperator
			)
		);
	}

	function validateWithValueCollection(valueCollectionName, key) {
		var valueCollection = parserInstance.getValueCollectionByRuleName(valueCollectionName);
		var lowerCaseValue;
		var i;

		key = key.toLowerCase();
		valueCollection = Array.isArray(valueCollection) ? valueCollection : Object.keys(valueCollection);

		for (i = 0; i < valueCollection.length; i++) {
			lowerCaseValue = valueCollection[i].toLowerCase();

			if (key === lowerCaseValue) {
				return true;
			}
		}

		return false;
	}

	function setupValidOptionNames(familyName) {
		var valueCollection = parserInstance.getValueCollectionByRuleName('OptionName');
		var validNames = {};
		var optionName;

		for (optionName in valueCollection) {
			if (valueCollection[optionName].indexOf(familyName) !== -1) {
				validNames[optionName] = true;
			}
		}

		parserInstance._ValidOptionNames = validNames;
	}

	function foldLogicOperationList(operator, head, tail) {
		if (tail.length) {
			var logicOperation;
			var i;

			for (i = 0; i < tail.length; i++) {
				logicOperation = tail[i][3];

				if (head.type === 'LogicExpression' && head.op === operator) {
					head.children.push(logicOperation);
				} else {
					head = {
						type: 'LogicExpression',
						op: operator,
						children: [head].concat(logicOperation)
					};
				}
			}
		}

		return head;
	}

	parserInstance.getValueCollectionByRuleName = function(ruleName) {
		var mappedCollection = usageNameValueCollectionMap[ruleName];

		return (typeof mappedCollection == 'string' ? this[mappedCollection] : mappedCollection) || [];
	};
}

// Lexem Analysis rules section
LexemAnalysis
	= Lexem*

Lexem
	= _ / OpenBracket / CloseBracket / LexemLogicOperatorLiteral / LexemNotLiteral / BooleanOperator / LexemFamilyName / LexemOptionName / OpenSquareBracket/ CloseSquareBracket / LexemUnknown

LexemLogicOperatorLiteral 'LogicOperatorLiteral'
	= lexem:('OR'i / 'AND'i) ws:_ {return [{type: 'LogicOperator', text: lexem}, ws]}

LexemNotLiteral 'NotLiteral'
	= lexem:'NOT'i ws:_ {return [{type: 'NotLiteral', text: lexem}, ws]}

LexemFamilyName 'FamilyName'
	= family:$noSpecialCharactersString & {
		return validateWithValueCollection('FamilyName', family);
	} {
		return {type: 'FamilyName', text: family}
	}
	/ open:OpenSquareBracket family:$noSquareBracketString close:CloseSquareBracket & {
		return validateWithValueCollection('FamilyName', family);
	} {
		return [{type: 'itembracket', text: open.text}, {type: 'FamilyName', text: family}, {type: 'itembracket', text: close.text}];
	}

LexemOptionName 'OptionName'
	= option:$noSpecialCharactersString & {
		return validateWithValueCollection('OptionName', option);
	} {
		return {type: 'ValidOptionName', text: option}
	}
	/ open:OpenSquareBracket option:$noSquareBracketString close:CloseSquareBracket & {
		return validateWithValueCollection('OptionName', option);
	} {
		return [{type: 'itembracket', text: open.text}, {type: 'ValidOptionName', text: option}, {type: 'itembracket', text: close.text}];
	}

// all unknown lexems should have 'unknown' type
LexemUnknown
	= lexem:$.+ {return {type: 'unknown', text: lexem}}

// Expression Analysis rules section
ExpressionAnalysis
	= ImplicationExpression / SimpleExpression

ImplicationExpression
	= _ LogicOrExpression _ LogicOrExpression

SimpleExpression
	= __ expression:LogicOrExpression {
		return expression;
	}

LogicOrExpression
	= head:LogicAndExpression tail:(_ ExpressionLogicOrOperatorLiteral _ LogicAndExpression)* {
		return foldLogicOperationList('OR', head, tail);
	}

LogicAndExpression
	= head:NotExpression tail:(_ ExpressionLogicAndOperatorLiteral _ NotExpression)* {
		return foldLogicOperationList('AND', head, tail);
	}

NotExpression
	= head:(ExpressionNotOperatorLiteral _)? expr:ParenthesisExpression {
		return head ? {type: 'LogicExpression', op: 'NOT', children: [expr]} : expr;
	} / head:(ExpressionNotOperatorLiteral _)? expr:BinaryExpression {
		return head ? {type: 'LogicExpression', op: 'NOT', children: [expr]} : expr;
	}

ParenthesisExpression
	= OpenBracket __ expr:SimpleExpression __ CloseBracket {
		return expr;
	}

BinaryExpression
	= left:ExpressionValidFamilyName __ operator:BooleanOperator __ right:ExpressionValidOptionName {
		return {type: 'BinaryExpression', op: operator.text, leftOperand: left, rightOperand: right};
	}

ExpressionLogicOrOperatorLiteral 'LogicOperator'
	= 'OR'i

ExpressionLogicAndOperatorLiteral 'LogicOperator'
	= 'AND'i

ExpressionNotOperatorLiteral 'NotLiteral'
	= 'NOT'i

OpenBracket 'OpenBracket'
	= '(' {return {type: 'bracket', text: text()}}

OpenSquareBracket 'OpenSquareBracket'
	= '[' {return {type: 'squarebracket', text: text()}}

CloseBracket 'CloseBracket'
	= ')' {return {type: 'bracket', text: text()}}

CloseSquareBracket 'CloseSquareBracket'
	= ']' {return {type: 'squarebracket', text: text()}}

BooleanOperator 'BooleanOperator'
	= ('=') {return {type: 'BooleanOperator', text: text()}}

ExpressionValidFamilyName
	= ExpressionFamilyName / OpenSquareBracket family:$BracketFamilyName CloseSquareBracket {
		return family;
	}

BracketFamilyName 'FamilyName'
	= family:$noSquareBracketString  & {
		return validateWithValueCollection('FamilyName', family);
	} {
		setupValidOptionNames(family);
		return {type: 'family', text: family};
	}

ExpressionFamilyName 'FamilyName'
	= family:$noSpecialCharactersString & {
		return validateWithValueCollection('FamilyName', family);
	} {
		setupValidOptionNames(family);
		return {type: 'family', text: family}
	}

ExpressionValidOptionName
	= ExpressionOptionName / OpenSquareBracket option:ExpressionBracketOptionName CloseSquareBracket {
		return option;
	}

ExpressionOptionName 'ValidOptionName'
	= option:$noSpecialCharactersString & {
		return validateWithValueCollection('ValidOptionName', option);
	} {
		return {type: 'option', text: option};
	}

ExpressionBracketOptionName 'ValidOptionName'
= option:$noSquareBracketString & {
	return validateWithValueCollection('ValidOptionName', option);
} {
	return {type: 'option', text: option};
}

noSquareBracketString 'noSquareBracketString'
	= [^\[\]]+

noSpecialCharactersString 'noSpecialCharactersString'
	= [0-9A-Za-z]+

_ 'whitespace'
	= [ \t\n\r]+ {return {type: 'whitespace', text: text()}}

__ 'softWhitespace'
	= ws:$[ \t\n\r]* {
		return ws ? {type: 'whitespace', text: ws} : null;
	}